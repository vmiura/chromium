# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")

component("ipc") {
  output_name = "cc_ipc"

  defines = [ "CC_IPC_IMPLEMENTATION" ]

  sources = [
    "cc_ipc_export.h",
    "cc_param_traits.cc",
    "cc_param_traits.h",
    "cc_param_traits_macros.h",
  ]

  public_deps = [
    "//cc",
    "//cc/surfaces",
    "//skia",
  ]

  deps = [
    "//base",
    "//gpu/ipc/common",
    "//ipc",
    "//ui/events/ipc",
    "//ui/gfx",
    "//ui/gfx/ipc",
    "//ui/gfx/ipc/color",
    "//ui/gfx/ipc/geometry",
    "//ui/gfx/ipc/skia",
  ]
}

mojom("image_decode_interfaces") {
  sources = [
    "image_decode.mojom",
  ]
  public_deps = [
    "//mojo/common:common_custom_types",
  ]
}

mojom("compositor_interfaces") {
  sources = [
    "element_id.mojom",
    "animation.mojom",
    "begin_frame_args.mojom",
    "compositor.mojom",
    "content_frame.mojom",
    "filter_operation.mojom",
    "filter_operations.mojom",
    "layer.mojom",
    "layer_tree_settings.mojom",
    "renderer_capabilities.mojom",
    "resource_format.mojom",
    "ui_resource.mojom",
  ]
  public_deps = [
    ":interfaces_surfaceid_only",
    "//gpu/ipc/common:interfaces",
    "//mojo/common:common_custom_types",
    "//skia/public/interfaces",
    "//ui/gfx/mojo",
  ]
}

mojom("interfaces") {
  sources = [
    "compositor_frame.mojom",
    "compositor_frame_metadata.mojom",
    "quads.mojom",
    "render_pass.mojom",
    "render_pass_id.mojom",
    "returned_resource.mojom",
    "selection.mojom",
    "shared_quad_state.mojom",
    "transferable_resource.mojom",
  ]

  public_deps = [
    ":compositor_interfaces",
    ":interfaces_surfaceid_only",
    "//gpu/ipc/common:interfaces",
    "//mojo/common:common_custom_types",
    "//skia/public/interfaces",
    "//ui/events/mojo:interfaces",
    "//ui/gfx/geometry/mojo",
    "//ui/gfx/mojo",
  ]
}

mojom("interfaces_surfaceid_only") {
  sources = [
    "surface_id.mojom",
  ]
}

mojom("test_interfaces") {
  testonly = true
  sources = [
    "traits_test_service.mojom",
  ]

  public_deps = [
    ":interfaces",
    ":interfaces_surfaceid_only",
  ]
}
